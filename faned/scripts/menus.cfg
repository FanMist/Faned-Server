//Fanatic Edition Menus

newgui about [
    guilist [
        guilist [
            guitext (concatword "^f7The ^f8Fanatic^f7 client for ^f8Fanatic^f7 people")
            guistrut 1
            guitext "^f7Cube 2: Sauerbraten - ^f8Fanatic Edition"
            guitext "^f7License: ^f8The zlib/libpng License"
            guitext "^f7Website: ^f8http://fanaticclan.com/"
            guistrut 1
            guitext "^f4Cube 2: Sauerbraten"
            guitext "^f4by Wouter ^"Aardappel^" van Oortmerssen, Lee ^"eihrul^" Salzman,"
            guitext "^f4Mike ^"Gilt^" Dysart, Robert ^"baby-rabbit^" Pointon,"
            guitext "^f4John ^"geartrooper^" Siar, Quinton ^"Quin^" Reeves, and others"
            guitext "^f4(for a full list of contributors see the readme)"
            guitext "^f4http://sauerbraten.org/"
        ]
    ]
]

newgui main [
    guititle "Cube 2"
    guibar
    guilist [
        guilist [
            guibutton "Singleplayer"   [showgui gamemode] "arrow_fw.jpg"
            guibutton "Multiplayer"    [showgui servers] "arrow_fw.jpg"
            guibutton "Demos"          [showgui demos] "arrow_fw.jpg"
            guibar
            guibutton "Statistics"     [showgui statistics] "arrow_fw.jpg"
            guibutton "IRC-Chat"       [showgui irc] "arrow_fw.jpg"
            guibutton "Options"        [showgui options] "arrow_fw.jpg"
            guibutton "About"          [showgui about] "info"
            guibutton "Quit"           [quit] "exit"
        ]
        if (isconnected) [
            guibar
            guilist [
                guibutton "Master Controls"  [showgui master]
                if (|| $editing (m_edit (getmode))) [
                    guibar
                    guibutton "Vslots"       [showgui texgui]
                    guibutton "Blender"      [showgui smc]
                    guibutton "Prefabs"      [showgui prefabs]
                    guibutton "Skyboxes"     [showgui skyboxes]
                    guibutton "Mapmodels"    [showgui mapmodels]
                    guibutton "Particles"    [showgui newparticles]
                    guibar
                    guibutton "Load Map"     [showgui maps]
                    guibutton "Color Picker" [showgui colorpicker]
                    guibutton "Editing Help" [showgui editing]
                    guibar
                ] [
                    guibutton "Vote Mode/Map" [showgui gamemode]
                ]
                guibutton "Toggle Spectator" [spectator (! (isspectator (getclientnum)))]
                if (m_teammode (getmode)) [
                    guibutton "Switch Team" [if (strcmp (getteam) "good") [team evil] [team good]]
                ]
                guibutton "Disconnect" [disconnect] "exit"
            ]
        ]
    ]
] 0

newgui demos [
    createine _demo_path "demos/"
    guilist [
        guitext "^f7Adjust directory^f7: "
        guifield _demo_path -68
    ]
    guistrut 1
    guitext (concatword "^f7Current .dmo files stored @ " $_demo_path) "info.jpg"
    guistrut 1
    loopfiles f $_demo_path "dmo" [guibutton (concatword "^f7watch ^f5/" $f ".dmo") [demo (concatword $_demo_path @f)]]
]

ffamaps1 = "aard3c academy akaritori alithia alloy aqueducts arbana bvdm_01 castle_trap collusion complex corruption curvedm curvy_castle darkdeath deathtek depot"
ffamaps2 = "dirtndust DM_BS1 dock douze duel7 duel8 dune elegy fanatic_quake force fragplaza frostbyte frozen fury guacamole gubo hades"
ffamaps3 = "hashi hog2 industry injustice island justice kalking1 katrez_d kffa killfactory kmap5 konkuri-to ksauer1 legazzo lostinspace masdm mbt10"
ffamaps4 = "mbt2 mbt9 memento metl2 metl3 metl4 moonlite neondevastation neonpanic nmp8 nucleus oasis oddworld ogrosupply orbe orion osiris"
ffamaps5 = "ot outpost paradigm park pgdm phosgene pitch_black powerplant refuge renegade rm5 roughinery ruby ruine sauerstruck sdm1 shadowed"
ffamaps6 = "shindou shinmei1 shiva simplicity skrdm1 stemple suburb tartech teahupoo tejen thetowers thor torment tumwalk turbine wake5 wdcd"
capturemaps1 = "abbey akroseum alithia arabic asgard asteroids c_egypt c_valley campo capture_night caribbean collusion core_refuge core_transfer corruption cwcastle damnation"
capturemaps2 = "dirtndust donya duomo dust2 eternal_valley evilness face-capture fb_capture fc3 fc4 fc5 forge frostbyte hades hallo haste hidden"
capturemaps3 = "infamy killcore3 kopenhagen lostinspace mbt12 mercury monastery nevil_c nitro nmp4 nmp8 nmp9 nucleus ogrosupply paradigm ph-capture reissen"
capturemaps4 = "relic river_c serenity snapper_rocks spcr subterra suburb tempest tortuga turbulence twinforts urban_c valhalla venice xenon"
ctfmaps1 = "abbey akroseum arbana asgard authentic autumn bad_moon berlin_wall bt_falls campo capture_night catch22 core_refuge core_transfer damnation desecration dust2"
ctfmaps2 = "eternal_valley europium evilness face-capture flagstone forge forgotten garden hallo haste hidden infamy kopenhagen l_ctf mach2 mbt1 mbt12"
ctfmaps3 = "mbt4 mercury mill nitro nucleus recovery redemption reissen sacrifice shipwreck siberia snapper_rocks spcr subterra suburb tejen tempest"
ctfmaps4 = "tortuga turbulence twinforts urban_c valhalla wdcd xenon"
conceptmaps = "ladder spiralz canyon secondevermap firstevermap door_demo box_demo platform_demo sauerbraten k_rpg1 rpg_01"
spmaps = "mpsp6a mpsp6b mpsp6c mpsp9a mpsp9b mpsp9c mpsp10 lost level9"
allmaps = (concat $ffamaps1 $ffamaps2 $ffamaps3 $ffamaps4 $ffamaps5 $ffamaps6 $capturemaps1 $capturemaps2 $capturemaps3 $capturemaps4 $ctfmaps1 $ctfmaps2 $ctfmaps3 $ctfmaps4 $conceptmaps $spmaps)

newgui gamemode [
    guibutton "Campaign"    [showgui campaign]
    guibar
    guilist [
        guilist [
            guibutton "ffa"                "mode 0; showgui maps"
            guibutton "coop-edit"          "mode 1; showgui maps"
            guibutton "teamplay"           "mode 2; showgui maps"
            guibutton "instagib"           "mode 3; showgui maps"
            guibutton "instagib team"      "mode 4; showgui maps"
            guibutton "efficiency"         "mode 5; showgui maps"
            guibutton "efficiency team"    "mode 6; showgui maps"
            guibutton "tactics"            "mode 7; showgui maps"
            guibutton "tactics team"       "mode 8; showgui maps"
            guibutton "capture"            "mode 9; showgui capturemaps"
            guibutton "regen capture"      "mode 10; showgui capturemaps"
        ]
        guibar
        guilist [
            guibutton "ctf"                "mode 11; showgui ctfmaps"
            guibutton "insta ctf"          "mode 12; showgui ctfmaps"
            guibutton "efficiency ctf"     "mode 17; showgui ctfmaps"
            guibutton "protect"            "mode 13; showgui ctfmaps"
            guibutton "insta protect"      "mode 14; showgui ctfmaps"
            guibutton "efficiency protect" "mode 18; showgui ctfmaps"
            guibutton "hold"               "mode 15; showgui capturemaps"
            guibutton "insta hold"         "mode 16; showgui capturemaps"
            guibutton "efficiency hold"    "mode 19; showgui capturemaps"
            guibutton "collect"            "mode 20; showgui ctfmaps"
            guibutton "insta collect"      "mode 21; showgui ctfmaps"
            guibutton "efficiency collect" "mode 22; showgui ctfmaps"
        ]
    ]
]

createine custom ""
createine mapindex 0

regenmaplist = [
    custom = ""
    loopfiles f packages/base ogz [
        match = 0
        loop j (listlen $ffamaps1) [
            if (strcmp $f (at $ffamaps1 $j)) [match = 1]
        ]
        loop j (listlen $ffamaps2) [
            if (strcmp $f (at $ffamaps2 $j)) [match = 1]
        ]
        loop j (listlen $ffamaps3) [
            if (strcmp $f (at $ffamaps3 $j)) [match = 1]
        ]
        loop j (listlen $ffamaps4) [
            if (strcmp $f (at $ffamaps4 $j)) [match = 1]
        ]
        loop j (listlen $ffamaps5) [
            if (strcmp $f (at $ffamaps5 $j)) [match = 1]
        ]
        loop j (listlen $ffamaps6) [
            if (strcmp $f (at $ffamaps6 $j)) [match = 1]
        ]
        loop j (listlen $capturemaps1) [
            if (strcmp $f (at $capturemaps1 $j)) [match = 1]
        ]
        loop j (listlen $capturemaps2) [
            if (strcmp $f (at $capturemaps2 $j)) [match = 1]
        ]
        loop j (listlen $capturemaps3) [
            if (strcmp $f (at $capturemaps3 $j)) [match = 1]
        ]
        loop j (listlen $capturemaps4) [
                        if (strcmp $f (at $capturemaps4 $j)) [match = 1]
        ]
        loop j (listlen $ctfmaps1) [
            if (strcmp $f (at $ctfmaps1 $j)) [match = 1]
        ]
        loop j (listlen $ctfmaps2) [
            if (strcmp $f (at $ctfmaps2 $j)) [match = 1]
        ]
        loop j (listlen $ctfmaps3) [
                        if (strcmp $f (at $ctfmaps3 $j)) [match = 1]
                ]
                loop j (listlen $ctfmaps4) [
                        if (strcmp $f (at $ctfmaps4 $j)) [match = 1]
                ]
        loop j (listlen $conceptmaps) [
            if (strcmp $f (at $conceptmaps $j)) [match = 1]
        ]
        loop j (listlen $spmaps) [
                        if (strcmp $f (at $spmaps $j)) [match = 1]
                ]
        if (! $match) [ custom = (format "%1 ^"%2^"" $custom $f) ]
    ]
]

showmapshot = [ 
    guibar
    pic = (concatword "packages/base/" (? (> $numargs 0) $arg1 (at $guirollovername 0)))
    ret = (findfile (format "%1%2" $pic ".jpg"))
    guiimage (concatword $pic (? (= $ret 0) ".png" ".jpg")) $guirolloveraction 4 1 "data/cube.png"
]

newgui maps [
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $ffamaps1 ]
        guilist [ guistrut 15 1; genmapitems $ffamaps2 ]
        showmapshot
    ]
    guitab 2
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $ffamaps3 ]
        guilist [ guistrut 15 1; genmapitems $ffamaps4 ]
        showmapshot
    ]
    guitab 3
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $ffamaps5 ]
        guilist [ guistrut 15 1; genmapitems $ffamaps6 ]
        showmapshot
    ]
    guitab capture
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $capturemaps1 ]
        guilist [ guistrut 15 1; genmapitems $capturemaps2 ]
        showmapshot
    ]
    guitab 2
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $capturemaps3 ]
        guilist [ guistrut 15 1; genmapitems $capturemaps4 ]
        showmapshot
    ]
    guitab ctf
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $ctfmaps1 ]
        guilist [ guistrut 15 1; genmapitems $ctfmaps2 ]
        showmapshot
    ]
    guitab 2
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $ctfmaps3 ]
        guilist [ guistrut 15 1; genmapitems $ctfmaps4 ]
        showmapshot
    ]

    guitab concept
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $conceptmaps ]
        guilist [ guistrut 15 1 ]
        showmapshot
    ]
    
    guitab SP
    guilist [
        guistrut 17 1
        guilist [guistrut 15 1; genmapitems $spmaps ]
        guilist [guistrut 15 1 ]
        showmapshot
    ]

    guitab "Custom"
    mapnum = (listlen $custom)
    mapindex = (max 0 (min $mapindex (- $mapnum (- $guiautotab 2))));
  
    guilist [
        if (> $mapnum (- $guiautotab 1)) [
            guislider mapindex (- $mapnum (- $guiautotab 1)) 0 "" 0
        ]
        guilist [
            guistrut 17 1
            guistayopen [guibutton "Refresh                  " regenmaplist]
            guibar

            loop i (- $guiautotab 1) [
                if (> $mapnum (+ $mapindex $i)) [
                    index = (+ $i $mapindex);
                    guibutton (at $custom $index)  (format "map %1"  (at $custom $index))
                ]
            ]
            if (! $mapnum) [guitext "No custom maps"]
        ]
        showmapshot
     ]
]

newgui servers [
    guistayopen [
        guiservers [
            guilist [
                guibutton "update from master server" "updatefrommaster"
                guibar
                guicheckbox "search LAN" searchlan
                guibar
                guicheckbox "auto-auth" autoauth
                guibar
                guicheckbox "auto-sort" autosortservers
                if (= $autosortservers 0) [
                    guibar
                    guibutton "sort" "sortservers"
                ]
                guibar
                guicheckbox "auto-update" autoupdateservers
                guibar
                guibutton "reconnect" [reconnect]
                guibar
                guibutton "favorites" [showgui favserv]
            ]
            guibar
        ] 17
    ]
] "" [initservers]

createine _ircnet "kerat.net"
createine _irchost "chat.kerat.net"
createine _ircport 6667
createine _ircchan "#fanatic"
createine _ircnick (getname)
createine _ircquitmsg "Powered by Cube 2: Fanatic Edition"

newgui irc [
    guistayopen [
        guititle "^f7Ingame IRC Settings"
        guistrut 1
        guilist [
            guilist [
                guitext "^f7Network:  "
                guitext "^f7Address:  "
                guitext "^f7Port:     "
                guitext "^f7Channel:  "
                guitext "^f7Nick:     "
                guitext "^f7Quit Msg: "
            ]
            guilist [
                _newircnet     = $_ircnet;     guifield _newircnet     -30 [_ircnet     = $_newircnet]
                _newirchost    = $_irchost;    guifield _newirchost    -30 [_irchost    = $_newirchost]
                _newircport    = $_ircport;    guifield _newircport    -30 [_ircport    = $_newircport]
                _newircchan    = $_ircchan;    guifield _newircchan    -30 [_ircchan    = $_newircchan]
                _newircnick    = $_ircnick;    guifield _newircnick    -30 [_ircnick    = $_newircnick]
                _newircquitmsg = $_ircquitmsg; guifield _newircquitmsg -30 [_ircquitmsg = $_newircquitmsg]
            ]
        ]
        guistrut 1
        guilist [
            guibutton "^f7Connect Network" [
                ircaddclient $_ircnet $_irchost $_ircport $_ircnick
                ircaddchan $_ircnet $_ircchan
                ircquitmsg $_ircquitmsg
                ircconnect $_ircnet
            ]
            guistrut 1; guibutton "^f7Join Channel" [ircjoinchan $_ircnet $_ircchan]
            guistrut 1; guibutton "^f7Disconnect IRC" [ircdisconnect]
        ]
    ]
] "IRC"

newgui statistics [
    guinoautotab [
        guilist [
            guilist [
                guitext (concatword "^f7Statistics for ^f8" (getname) "^f7 (^f8"(getclientnum) "^f7) ")
                guistrut 1
                guitext (concatword "^f7Current Uptime: ^f8" (? (< (div (getmillis) 3600000) 10) 0) (div (getmillis) 3600000) ":" (? (< (mod (div (getmillis) 60000) 60) 10) 0) (mod (div (getmillis) 60000) 60) ":" (? (< (mod (div (getmillis) 1000) 60) 10) 0) (mod (div (getmillis) 1000) 60))
                guitext (concatword "^f7Current Model: ^f8" (playermodelname))
                if (isconnected && getteam) [guitext (concatword "^f7Current Team: ^f8" (getteam))]
                guitext (concatword "^f7Current Health: ^f8" (gethealth))
                guitext (concatword "^f7Current Armour: ^f8" (getarmour))
                guitext (concatword "^f7Current Weapon: ^f8" (if (= (getweapon) 0) [concatword "Chainsaw ^f7(^f80^f7)"] [if (= (getweapon) 1) [concatword "Shotgun ^f7 (^f81^f7)"] [if (= (getweapon) 2) [concatword "Chaingun ^f7(^f82^f7)"] [if (= (getweapon) 3) [concatword "Rocket Launcher ^f7(^f83^f7)"] [if (= (getweapon) 4) [concatword  "Rifle ^f7(^f84^f7)"] [if (= (getweapon) 5) [concatword "Grenade Launcher ^f7(^f85^f7)"] [if (= (getweapon) 6) [concatword "Pistol ^f7(^f86^f7)"]]]]]]]))
                guitext (concatword "^f7Current Shots: ^f8" (div (gettotalshots) 100))
                guitext (concatword "^f7Current Frags: ^f8" (getfrags))
                guitext (concatword "^f7Current Deaths: ^f8" (getdeaths))
                guitext (concatword "^f7Current KPD: ^f8" (substr (divf (max 0 (getfrags)) (max 1 (getdeaths))) 0 5))
                guitext (concatword "^f7Current DDPD: ^f8" (substr (divf (gettotaldamage) (max 1 (getdeaths))) 0 5))
                guitext (concatword "^f7Current Damage Dealt: ^f8" (gettotaldamage))
                guitext (concatword "^f7Current Damage Wasted: ^f8" (- (gettotalshots) (gettotaldamage)))
                guitext (concatword "^f7Current Damage Total: ^f8" (gettotalshots))
                guitext (concatword "^f7Current Accuracy: ^f8" (getaccuracy) "^f7%")
                if (m_ctf (getmode)) [guitext (concatword "^f7Current Flags: ^f8" (getflags))]
                if (m_collect (getmode)) [guitext (concatword "^f7Current Skulls: ^f8" (getflags))]
                if (mapname) [guitext (concatword "^f7Current Map: ^f8" (mapname) " ^f7(^f8" (if (< (timeremaining) 1) [concatword "intermission"] [concatword "-" (timeremaining 1)]) "^f7)")]
                if (mapname) [guitext (concatword "^f7Current Mode: ^f8" (at $modenames (getmode)))]
                guitext (concatword "^f7Current Ping: ^f8" (getping))
                guitext (concatword "^f7Current FPS: ^f8" (getfps))
                if (isconnected) [
                    guitext (concatword "^f7Current Server: ^f8" (connectedip) "^f7:^f8" (connectedport) " ^f7(^f8" (getmastermode) "^f7)")
                ]
            ]
            guiplayerpreview $playermodel 0 6 $guirolloveraction 9
        ]
    ]
] "statistics"

_fullblinkmodels_state = 0

createine _bloodcolor_red 0
createine _bloodcolor_green 0
createine _bloodcolor_blue 0

createine _shotcolor_red 0
createine _shotcolor_green 0
createine _shotcolor_blue 0

newgui options [
    guistayopen [ 
        guinoautotab [
            guitext "^f7gui"
            guibar
            guicheckbox "2D menus" gui2d
            guibar
            guitext "^f7head up display"
            guibar
            guicheckbox "SauerEnhanced HUD" sehud
            if (! $sehud) [
                guibar
                guicheckbox "show fps" showfps
                guicheckbox "show speed" showspeed
            ] 
            guilist [ 
                if (! $sehud) [ 
                    guicheckbox "show wall clock" wallclock 
                    guibar 
                    guicheckbox "24 hour" wallclock24 
                    guibar 
                    guicheckbox "seconds" wallclocksecs 
                ] 
            ] 
            guibar 
            guitext "^f7scoreboard" 
            guibar 
            guilist [ 
                guicheckbox "deathscore "   deathscore 
                guibar 
                guicheckbox "show cn"       showclientnum 
                guibar 
                guicheckbox "show ping"     showping 
                guibar 
                guicheckbox "show pj"       showpj 
            ] 
            guibar 
            guitext "^f7players" 
            guibar 
            guilist [ 
                guicheckbox "fullbright player models" fullbrightmodels 60 0 
                if $fullbrightmodels [ 
                    guibar 
                    guiradio "subtle" fullbrightmodels 60 
                    guibar 
                    guiradio "bright" fullbrightmodels 100 
                    guibar 
                    guiradio "overbright" fullbrightmodels 150 
                    guibar 
                    guiradio "max" fullbrightmodels 200 
                    guibar 
                    if (= $_fullblinkmodels_state 1) [ 
                        guibutton "blink" [fullblinkmodels 0] "checkbox_on.jpg" 
                    ] [ 
                        guibutton "blink" [fullblinkmodels 1] "checkbox_off.jpg" 
                    ] 
                    if (= $_faned_visor 0) [guibar; guicheckbox "force own" forceplayermodels] 
                    guibar 
                    guicheckbox "force team" teamskins 
                    guibar 
                    if (= $_faned_visor 1) [ 
                        guibutton "visor mode" [_faned_visor = 0; removezip "faned/skins/visor.zip"; playermodel 1; forceplayermodels 0; resetgl] "checkbox_on.jpg" 
                    ] [ 
                        guibutton "visor mode" [_faned_visor = 1; addzip "faned/skins/visor.zip"; playermodel 1; forceplayermodels 1; resetgl] "checkbox_off.jpg" 
                    ] 
                ] 
            ] 
            guibar 
            guitext "^f7crosshair" 
            guibar 
            guilist [ 
                guitext "^f7mouse sensitivity: " 
                newsens = $sensitivity 
                guifield newsens 8 [sensitivity $newsens] 
                guitext "^f7mouse UI sensitivity: " 
                newguisens = $guisens 
                guifield newguisens 8 [guisens $newguisens] 
                guibar 
                guicheckbox "^f7invert mouse" invmouse 
            ] 
            guilist [ 
                guibutton "crosshair: " [showgui crosshair] "arrow_fw.jpg" 
                guiimage (getcrosshair) [showgui crosshair] 0.5 
                guibar 
                guicheckbox "crosshair color effects" crosshairfx 
                if $crosshairfx [ 
                    guibar 
                    guicheckbox "teammates" teamcrosshair 
                    guibar 
                    guicheckbox "hits" hitcrosshair 425 
                ] 
                guibar 
                guicheckbox "hit sound" hitsound 
            ] 
            guibar 
            guitext "^f7do not disturb" 
            guibar 
            guilist [ 
                guicheckbox "receive ai"       receiveai
                guibar 
                guicheckbox "receive newmap"   receivenewmap 
                guibar 
                guicheckbox "receive remip"    receiveremip 
                guibar 
                guicheckbox "receive sendmap"  receivesendmap 
            ] 
            guibar 
            guitext "^f7anti-cheat" 
            guibar 
            if $detect [ 
                guibutton "^f0disable anti-cheat detection " [detect 0] "checkbox_on" 
            ] [ 
                guibutton "^f3enable anti-cheat detection" [detect 1] "checkbox_off" 
            ] 
     
            guitab    "action" 
            guilist [ 
                guitext "^f7damage" 
                guibar 
                guicheckbox "^f7automatic respawn after death" autorespawn 
            ] 
            guibar 
            guilist [ 
                guicheckbox "blood"             blood 
                guibar 
                guicheckbox "damage compass"    damagecompass 
                guibar 
                guicheckbox "damage screen"     damagescreen 
                guibar 
                if (= $deathpanicscreen 1) [ 
                    guibutton "deathpanic effect" [deathpanicsound 0; deathpanicscreen 0] "checkbox_on.jpg" 
                ] [ 
                    guibutton "deathpanic effect" [deathpanicsound 1; deathpanicscreen 1] "checkbox_off.jpg" 
                ]       
            ] 
            guibar 
            _set_bloodcolor = [bloodcolor (+ (* (+ (* $_bloodcolor_red 256) $_bloodcolor_green) 256) $_bloodcolor_blue)] 
            guitext "^f7blood color: " 
            guislider _bloodcolor_red   0 255 [_set_bloodcolor]  (+ (* (+ (* $_bloodcolor_red 256) $_bloodcolor_green) 256) $_bloodcolor_blue) 
            guislider _bloodcolor_green 0 255 [_set_bloodcolor]  (+ (* (+ (* $_bloodcolor_red 256) $_bloodcolor_green) 256) $_bloodcolor_blue) 
            guislider _bloodcolor_blue  0 255 [_set_bloodcolor]  (+ (* (+ (* $_bloodcolor_red 256) $_bloodcolor_green) 256) $_bloodcolor_blue) 
            guitext "^f7blood intensity:" 
            guislider bloodintensity 0 10 
            guibar 
            guilist [ 
                guitext "^f7hudguns" 
                guibar 
                guicheckbox "automatic weapon switch on ammo pickup" pickupautoswitch 
            ] 
            guibar 
            guilist [ 
                guicheckbox "enable hudguns"    hudgun 
                if $hudgun [ 
                    guibar 
                    guicheckbox "zoom scope (rifle)"    [zoomscopescreen] 
                    guibar 
                    guicheckbox "swag"          [hudgunswag]  "arrow_fw.jpg" 
                    guibar 
                    guicheckbox "sway"          [hudgunsway]  "arrow_fw.jpg" 
                    guibar 
                    guicheckbox "muzzle flash"  [muzzleflash] "arrow_fw.jpg" 
                    guibar 
                    guicheckbox "muzzle light"  [muzzlelight] "arrow_fw.jpg" 
                    guibar 
                    guicheckbox "sparks"        [shotsparks]  "arrow_fw.jpg" 
                ] 
            ] 
            guilist [ 
                guitext "^f7hudgunsdir: " 
                guibutton "default"    [_faned_gunz = 0; hudgunsdir ""; removezip "faned/guns/hudguns_gunz.zip"; resetgl] "arrow_fw.jpg" 
                guibar 
                guibutton "classic"    [_faned_gunz = 0; hudgunsdir "../../faned/guns/hudguns_classic"] "arrow_fw.jpg" 
                guibar 
                guibutton "ctf"        [_faned_gunz = 0; hudgunsdir "../../faned/guns/hudguns_ctf"] "arrow_fw.jpg" 
                guibar 
                guibutton "ctf2"       [_faned_gunz = 0; hudgunsdir "../../faned/guns/hudguns_ctf2"] "arrow_fw.jpg" 
                guibar 
                guibutton "gunz"       [_faned_gunz = 1; hudgunsdir ""; addzip "faned/guns/hudguns_gunz.zip"; resetgl] "arrow_fw.jpg" 
                guibar 
                guibutton "solaris"    [_faned_gunz = 0; hudgunsdir "../../faned/guns/hudguns_solaris"; resetgl] "arrow_fw.jpg" 
            ] 
            guibar 
            guitext "^f7smoke" 
            guibar 
            _set_shotcolor = [shotcolor (+ (* (+ (* $_shotcolor_red 256) $_shotcolor_green) 256) $_shotcolor_blue)] 
            guitext "^f7smoke color: " 
            guislider _shotcolor_red   0 255 [_set_shotcolor] (+ (* (+ (* $_shotcolor_red 256) $_shotcolor_green) 256) $_shotcolor_blue) 
            guislider _shotcolor_green 0 255 [_set_shotcolor] (+ (* (+ (* $_shotcolor_red 256) $_shotcolor_green) 256) $_shotcolor_blue) 
            guislider _shotcolor_blue  0 255 [_set_shotcolor] (+ (* (+ (* $_shotcolor_red 256) $_shotcolor_green) 256) $_shotcolor_blue) 
            if ($shotcolorrainbow) [ 
                guicheckbox "^fXrainbow smoke" shotcolorrainbow 
            ] [ 
                guicheckbox "^f7rainbow smoke" shotcolorrainbow 
            ] 
            guibar 
            guitext "^f7ragdolls" 
            guibar 
            guilist [ 
                guicheckbox "ragdoll deaths"    ragdoll 
                if $ragdoll [ 
                    guibar 
                    guicheckbox "keep after respawn" ragdollmillis 10000 
                ] 
                guibar 
                guicheckbox "hide dead players" hidedead 
            ] 
            guitext "^f7ragdoll velocity multiplier: " 
            guislider deadpush 
     
            guitab    "gfx" 
            guitext "performance key: ^f0fast^f~, ^f2moderate^f~, ^f3slow and pretty^f~" 
            guibar 
            if (&& (< $shaders 0) (= $renderpath 0)) [ 
                guicheckbox "^f2shaders        "            shaders 1 -1 
            ] [ 
                guilist [ 
                    guicheckbox "^f2shaders        "           shaders 
                    if $shaders [ 
                        guibar 
                        guiradio "^f0low detail" shaderdetail 1 
                        guibar 
                        guiradio "^f2high detail" shaderdetail 3 
                        if $hasglsl [ 
                            guibar 
                            guicheckbox "^f3GLSL only" forceglsl 
                        ] 
                    ] 
                ] 
            ] 
            guilist [ 
                guitext "water        " 
                guibar 
                guicheckbox "^f2refraction"  waterrefract 
                guibar 
                guicheckbox "^f3reflection"  waterreflect 
                guibar 
                guicheckbox "^f0caustics"    caustics 
                guibar 
                guicheckbox "^f0animation" vertwater 
            ] 
            if (> $renderpath 0) [ 
                guilist [ 
                    guitext "waterfalls        " 
                    guibar 
                    guicheckbox "^f2refraction" waterfallrefract 
                    guibar 
                    guicheckbox "^f0reflection" waterfallenv 
                ] 
            ] 
            if (= $renderpath 0) [ 
                guilist [ 
                    guicheckbox "^f3shadow maps    "  shadowmap 
                    if $shadowmap [ 
                        guibar 
                        guiradio "^f2medium quality" shadowmapsize 9 [blurshadowmap 1] 
                        guibar 
                        guiradio "^f3high quality" shadowmapsize 10 [blurshadowmap 2] 
                    ] [ 
                        guibar 
                        guicheckbox "^f0blob shadows" blobs 
                    ] 
                ] 
                if (>= $maxtmus 3) [ 
                    guicheckbox "^f2dynamic lights  "    ffdynlights 5 0 
                ] 
            ] [ 
                guilist [ 
                    guicheckbox "^f3soft shadows    "  shadowmap 
                    if $shadowmap [ 
                        guibar 
                        guiradio "^f2medium quality" shadowmapsize 9 [blurshadowmap 1] 
                        guibar 
                        guiradio "^f3high quality" shadowmapsize 10 [blurshadowmap 2] 
                    ] [ 
                        guibar 
                        guicheckbox "^f0blob shadows" blobs 
                    ] 
                ] 
                if $glare [ 
                    glarepreset = 0 
                    if (= $glarescale 1) [ 
                        if (= $blurglare 4) [glarepreset = 1] 
                        if (= $blurglare 7) [glarepreset = 3] 
                    ] 
                    if (= $glarescale 2) [ 
                        if (= $blurglare 3) [glarepreset = 2] 
                        if (= $blurglare 7) [glarepreset = 4] 
                    ] 
                    guilist [ 
                        guicheckbox "^f3glare            "       glare 
                        guibar 
                        guiradio "^f2subtle" glarepreset 1 [blurglare 4; glarescale 1] 
                        guibar 
                        guiradio "^f2glowy" glarepreset 2 [blurglare 3; glarescale 2] 
                        guibar 
                        guiradio "^f3soft" glarepreset 3 [blurglare 7; glarescale 1] 
                        guibar 
                        guiradio "^f3intense" glarepreset 4 [blurglare 7; glarescale 2] 
                    ] 
                ] [ 
                    guicheckbox "^f3glare"         glare 
                ] 
            ] 
            if $usetexrect [ 
                guilist [ 
                    guicheckbox "^f3motion blur    " motionblur 
                    if $motionblur [ 
                        guibar 
                        guiradio "^f3subtle" motionblurscale 0.5 
                        guibar 
                        guiradio "^f3moderate" motionblurscale 0.65 
                        guibar 
                        guiradio "^f3intense" motionblurscale 0.8 
                    ] 
                ] 
            ] 
            guilist [ 
                guicheckbox "^f3grass            " grass 
                if $grass [ 
                    guibar 
                    guiradio "^f2quick fade" grassdist 128 
                    guibar 
                    guiradio "^f2moderate fade" grassdist 256 
                    guibar 
                    guiradio "^f3slow fade" grassdist 512 
                ] 
            ] 
            if (> $renderpath 0) [ 
                guilist [ 
                    guicheckbox "^f0dynamic lights    "    maxdynlights 3 0 
                    if $maxdynlights [ 
                        guibar 
                        guiradio "^f0medium quality" maxdynlights 3 
                        guibar 
                        guiradio "^f2high quality" maxdynlights 5 
                    ] 
                ] 
                guilist [ 
                    guicheckbox "^f0soft particles    " depthfx 
                    if $depthfx [ 
                        guibar 
                        guiradio "^f0low quality" depthfxsize 7 [depthfxrect 0; depthfxfilter 1; blurdepthfx 1] 
                        guibar 
                        guiradio "^f2medium quality" depthfxsize 10 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0] 
                        guibar 
                        guiradio "^f3high quality" depthfxsize 12 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0] 
                    ] 
                ] 
            ] 
            guicheckbox "^f0glass reflection"  glassenv 
            guilist [ 
                guicheckbox "^f0decals        " decals 
                if $decals [ 
                    guibar 
                    guiradio "^f0quick fade" decalfade 10000 [maxdecaltris 1024] 
                    guibar 
                    guiradio "^f2slow fade" decalfade 60000 [maxdecaltris 4096] 
                ] 
            ] 
            guicheckbox "^f0fix t-joints (world sparklies)" filltjoints 
            guilist [ 
                guitext "textures        " 
                guibar 
                guiradio "^f0low quality" maxtexsize 256 
                guibar 
                guiradio "^f0medium quality" maxtexsize 512 
                guibar 
                guiradio "^f2high quality" maxtexsize 0 
            ] 
            guilist [ 
                guitext "models        " 
                guibar 
                guicheckbox "^f0lighting"    lightmodels 
                guibar 
                guicheckbox "^f0reflection"  envmapmodels 
                guibar 
                guicheckbox "^f0glow"        glowmodels 
                if (> $renderpath 0) [ 
                    guibar 
                    guicheckbox "^f2bumpmap" bumpmodels 
                ] 
            ] 
            guilist [ 
                guitext "animation        " 
                guibar 
                guiradio "^f0medium quality" matskel 1 
                guibar 
                guiradio "^f0high quality" matskel 0 
            ] 
         
            guitab    "display" 
            guicheckbox "v-sync"            vsync 1 0 
            guicheckbox "fullscreen"        fullscreen 
            guitext "gamma (default: 100)" 
            guislider gamma 
            guitext "full-scene anti-aliasing (default: -1)" 
            guilistslider fsaa "-1 0 2 4 8 16" 
            guitext "color depth (default: 0)" 
            guilistslider colorbits "0 16 24 32" 
            guitext "z-buffer depth (default: 0)" 
            guilistslider depthbits "0 16 24 32" 
            guitext "stencil bits (default: 0)" 
            guislider stencilbits 
            guitext "anisotropic filtering (default: 0)" 
            guilistslider aniso "0 2 4 8 16" 
            guilist [ 
                guicheckbox "bilinear filtering" bilinear 
                guibar 
                guicheckbox "trilinear filtering (mipmaps)" trilinear 
            ] 
         
            guitab   "res" 
            guitext "field of view (default: 100)" 
            guislider fov 
            guistayopen [ 
              guilist [ 
                guilist [ 
                    guitext "4:3" 
                    @@@@(resbutton 320 240) 
                    @@@@(resbutton 640 480) 
                    @@@@(resbutton 800 600) 
                    @@@@(resbutton 1024 768) 
                    @@@@(resbutton 1152 864) 
                    @@@@(resbutton 1280 960) 
                    @@@@(resbutton 1400 1050) 
                    @@@@(resbutton 1600 1200) 
                    @@@@(resbutton 1792 1344) 
                    @@@@(resbutton 1856 1392) 
                    @@@@(resbutton 1920 1440) 
                    @@@@(resbutton 2048 1536) 
                    @@@@(resbutton 2800 2100) 
                    @@@@(resbutton 3200 2400) 
                ] 
                guibar 
                guilist [ 
                    guitext "16:10" 
                    @@@@(resbutton 320 200) 
                    @@@@(resbutton 640 400) 
                    @@@@(resbutton 1024 640) 
                    @@@@(resbutton 1280 800) 
                    @@@@(resbutton 1440 900) 
                    @@@@(resbutton 1600 1000) 
                    @@@@(resbutton 1680 1050) 
                    @@@@(resbutton 1920 1200) 
                    @@@@(resbutton 2048 1280) 
                    @@@@(resbutton 2560 1600) 
                    @@@@(resbutton 2880 1800) 
                    @@@@(resbutton 3200 2000) 
                    @@@@(resbutton 3840 2400) 
                ] 
                guibar 
                guilist [ 
                    guitext "16:9" 
                    @@@@(resbutton 1024 600) 
                    @@@@(resbutton 1280 720) 
                    @@@@(resbutton 1366 768) 
                    @@@@(resbutton 1600 900) 
                    @@@@(resbutton 1920 1080) 
                    @@@@(resbutton 2048 1152) 
                    @@@@(resbutton 2560 1440) 
                    @@@@(resbutton 2880 1620) 
                    @@@@(resbutton 3200 1800) 
                    @@@@(resbutton 3840 2160) 
                ] 
                guibar 
                guilist [ 
                    guitext "5:4" 
                    @@@@(resbutton 600 480) 
                    @@@@(resbutton 1280 1024) 
                    @@@@(resbutton 1600 1280) 
                    @@@@(resbutton 2560 2048) 
                ] 
                guibar 
                guilist [ 
                    guitext "5:3" 
                    @@@@(resbutton 800 480) 
                    @@@@(resbutton 1280 768) 
         
                    guibar 
                    guitext "Custom" 
                    guilist [ 
                        customw = $scr_w 
                        customh = $scr_h 
                        guifield customw 4 [scr_w $customw] 
                        guifield customh 4 [scr_h $customh] 
                    ] 
                ] 
              ] 
            ] 
         
            guitab   "sound" 
            guitext "sound volume" 
            guislider soundvol 
            guitext "music volume" 
            guislider musicvol 
            guitext "sound channels" 
            guislider soundchans 
            guitext "sound frequency" 
            guilistslider soundfreq "11025 22050 44100" 
            guitext "sound buffer length" 
            guislider soundbufferlen 
            guicheckbox "Mumble positional audio" mumble 
         
            guitab "keys" 
            guitext "(basic keybinds, for anything more use the 'bind' command)" 
            guitext "select action to bind and press desired keys (ESC when done):" 
         
            guilistsplit n 2 $bindactions [ 
                guilist [ 
                    guitext (tabify (concatword $n ": ") 4) 
                    [newbinds@i] = (searchbinds $n) 
                    guikeyfield [newbinds@i] -9 [ 
                        oldbinds = (searchbinds [@@n]) 
                        looplist j $oldbinds [bind $j ""] 
                        looplist j $[newbinds@@i] [bind $j [@@@n]] 
                    ] 
                ] 
            ] [guibar] 
         
            guitab "console" 
            guicheckbox "chat console" miniconfilter 0x300 0 
            guibar
            guitext "console size (lines)" 
            guislider consize 
            guibar
            guitext "console filter:" 
            guibar
            guilist [ 
                guilist [ 
                    guibitfield "important information" confilter 0x01
                    guibitfield "warnings" confilter 0x02
                    guibitfield "errors" confilter 0x04
                    guibitfield "irc messages" confilter 0x40
                    guibitfield "init messages" confilter 0x10
                    guibitfield "script messages" confilter 0x20
                ] 
                guibar 
                guilist [ 
                    guibitfield "chat" confilter 0x100 
                    guibitfield "team chat" confilter 0x200 
                    guibitfield "game events" confilter 0x400 
                    guibitfield "self frags" confilter 0x800 
                    guibitfield "other player frags" confilter 0x1000 
                    guibitfield "team kills" confilter 0x2000 
                ] 
            ] 
            guibar
            guitext "full console size (percent of screen)" 
            guislider fullconsize 
            guibar
            guitext "full console filter:" 
            guibar
            guilist [ 
                guilist [ 
                    guibitfield "important information" fullconfilter 0x01 
                    guibitfield "warnings" fullconfilter 0x02 
                    guibitfield "errors" fullconfilter 0x04 
                    guibitfield "irc messages" fullconfilter 0x40
                    guibitfield "init messages" fullconfilter 0x10 
                    guibitfield "script messages" fullconfilter 0x20 
                ] 
                guibar 
                guilist [ 
                    guibitfield "chat" fullconfilter 0x100 
                    guibitfield "team chat" fullconfilter 0x200 
                    guibitfield "game events" fullconfilter 0x400 
                    guibitfield "self frags" fullconfilter 0x800 
                    guibitfield "other player frags" fullconfilter 0x1000 
                    guibitfield "team kills" fullconfilter 0x2000 
                ] 
            ] 
     
            guitab "autoexec.cfg" 
            guitext "autoexec.cfg" 
            guistrut 1 
            showfileeditor "autoexec.cfg" -60 15 
     
            guitab "sounds.cfg" 
            guitext "sounds.cfg: Adjust sound and volume (0-255) for default game events:" 
            guistrut 1 
            guieditor faned/scripts/sounds.cfg -60 15 
            textinit faned/scripts/sounds.cfg faned/scripts/sounds.cfg
            guilist [ 
                guibutton "load" [textfocus @arg1; textload @arg1]
                guibar
                guibutton "save" [ 
                    textfocus @arg1; textsave @arg1; 
                    showgui _faned_restart 
                ] "arrow_fw.jpg" 
                guibar
                guibutton "exec" [textfocus @arg1; textexec] 
                guibar
                guibutton "copy" [textfocus @arg1; textcopy] 
                guibar
                guibutton "paste" [textfocus @arg1; textpaste] 
                guibar
                guibutton "select" [textfocus @arg1; textselectall] 
                guibar
                guibutton "clear" [textfocus @arg1; textclear] 
            ]
        ]
    ] 
] "game"

contexteditgui = [
    if $hmapedit [showgui brushes] [
        if (enthavesel) [
            showgui (enttype)
        ] [showgui quickedit]
    ]
]; showentgui = [contexteditgui]

quickeditmenu = [
    guinoautotab [
        guititle "Quick Commands:"
        guibar
        guilist [
            guibutton "^f0getmap" [getmap]
            guistrut 1
            guibutton "^f2sendmap" [sendmap]
            guistrut 1
            guibutton "^f3savemap" [savemap]
        ]
        guibar
        guitext "^f7calclight radius: "
        guislider calclightradius 0 1000
        guitext "^f4[ set 0 to disable local calclight ]"
        guibar
        guibutton "calclight low (-1)"    [calclight -1]
        guibutton "calclight default (0)" [calclight   ]
        guibutton "calclight high (1)"    [calclight  1]
        guibutton "check lights" [
            if (|| [entfind light; enthavesel] [!=s $ambient 1644825] [!= $skylight 0] [!= $sunlight 0]) [
                echo "^f7lights detected"
            ] [
                echo "^f7no lights detected"
            ]; enttoggle
        ]
        guibutton "clear lightmaps" [clearlightmaps]
        guibutton "patchlight" [patchlight]
        guibutton "recalc" [recalc]
        guibutton "remip" [remip]
        guistrut 1
    ]
    if $havesel [
        guitab "materials"
        guicheckbox "show material volumes"    showmat
        guibar
        guibutton "air"                        "editmat air"
        guibutton "water"                      "editmat water"
        guibutton "lava"                       "editmat lava"
        guibutton "clip"                       "editmat clip"
        guibutton "glass"                      "editmat glass"
        guibutton "noclip"                     "editmat noclip"
        guibutton "gameclip"                   "editmat gameclip"
        guibutton "death"                      "editmat death"
        guibutton "alpha"                      "editmat alpha"

        guitab "vslots"
        guilist [
            loop x 5 [
                texname = (gettexname (getseltex) $x)
                texname = (substr $texname (+ (strstr $texname ">") 1))
                guiimage (concatword "packages/" $texname) "" 1 1
            ]
            guilist [
                guitext (concatword "^f7index: " (max 0 (getseltex)))
                guilist [
                    guitext (concatword "^f7name: " (gettexname (getseltex)))
                ]
            ]
        ]
        guibar
        _gvalpha = (getvalpha (getseltex))
        guilist [guifield _gvalpha [valpha $_gvalpha]; guitext "^f7valpha"]
        guibar
        _gvcolor = (getvcolor (getseltex))
        guilist [guifield _gvcolor [vcolor $_gvcolor]; guitext "^f7vcolor"]
        guibar
        _gvlayer = (getvlayer (getseltex))
        guilist [guifield _gvlayer [vlayer $_gvlayer]; guitext "^f7vlayer"]
        guibar
        _gvoffset = (getvoffset (getseltex))
        guilist [guifield _gvoffset [voffset $_gvoffset]; guitext "^f7voffset"]
        guibar
        _gvrotate = (getvrotate (getseltex))
        guilist [guifield _gvrotate [vrotate $_gvrotate]; guitext "^f7vrotate"]
        guibar
        _gvscale = (getvscale (getseltex))
        guilist [guifield _gvscale [vscale $_gvscale]; guitext "^f7vscale"]
        guibar
        _gvscroll = (getvscroll (getseltex))
        guilist [guifield _gvscroll [vscroll $_gvscroll]; guitext "^f7vscroll"]
    ]
]

createine _colorpicker_red 0
createine _colorpicker_green 0
createine _colorpicker_blue 0

newgui colorpicker [
    guilist [
        guitext "Hexadecimal: " "info.jpg"
        guicolor (+ (* (+ (* $_colorpicker_red 256) $_colorpicker_green) 256) $_colorpicker_blue)
    ]
    guitext (concatword "Normalized: " (divf $_colorpicker_red 255) " " (divf $_colorpicker_green 255) " " (divf $_colorpicker_blue 255)) "info.jpg"
    guitext (concatword "Integer: " (+ (+ (<< $_colorpicker_red 16) (<< $_colorpicker_green 8)) $_colorpicker_blue)) "info.jpg"
    guitext "RGB:" "info.jpg"
    guislider _colorpicker_red   0 255 "" (+ (* (+ (* $_colorpicker_red 256) $_colorpicker_green) 256) $_colorpicker_blue)
    guislider _colorpicker_green 0 255 "" (+ (* (+ (* $_colorpicker_red 256) $_colorpicker_green) 256) $_colorpicker_blue)
    guislider _colorpicker_blue  0 255 "" (+ (* (+ (* $_colorpicker_red 256) $_colorpicker_green) 256) $_colorpicker_blue)
] "colorpicker"

newentgui = [
    genentattributes $arg1 $arg2 $arg3
    newgui $arg1 [
        guititle $entguitype
        guibar
        @entattributes
        @arg4
        guitab type
        guilistsplit n 2 $enttypelist [
            guibutton $n [entset @n]
        ]
    guitab misc
    @quickeditmenu
    ] "" [initentgui; @arg5]
]

looplist i $enttypelist [
    newentgui $i "" ""
]

newgui brushes [
    @brushmenu
]

newgui quickedit [
    quickeditmenu
] "misc"

entscale = 100

teamselect = [
    guitext "" 0
    guiradio "neural" entguiattr1 0 [entupdate]
    guiradio "team one" entguiattr1 1 [entupdate]
    guiradio "team two" entguiattr1 2 [entupdate]
]

newentgui base "" "" [
    guistayopen [
        guitext "tag"
        guislider entguiattr2 0 10
        guitext "" 0
        guilist [
            guilist [
            guiradio "shells   " entguiattr1 1 [entupdate] 
            guiradio "bullets  " entguiattr1 2 [entupdate]
            guiradio "rockets  " entguiattr1 3 [entupdate]
            guiradio "random   " entguiattr1 0 [entupdate] 
            ]
            guibar
            guilist [
                guiradio "rifle    " entguiattr1 4 [entupdate] 
                guiradio "grenades " entguiattr1 5 [entupdate]
                guiradio "pistol   " entguiattr1 6 [entupdate]
            ]
        ]
    ]
]

newentgui flag "direction" "0 360" [@teamselect]

halflight = [
    entguiattr1 = (min 255 (div (* ($entguiattr1) $arg1) $arg2))
    entguiattr2 = (min 255 (div (* ($entguiattr2) $arg1) $arg2))
    entguiattr3 = (min 255 (div (* ($entguiattr3) $arg1) $arg2))
    entupdate
]

newentgui light "radius red green blue" "0 400 0 255 0 255 0 255" [
    guistayopen [
        guibar
        guilist [
            guibutton "1/2 light  " [halflight 1 2 ] 
            guibar
            guibutton  "  " "" 0
            guibutton "3/4 light " [halflight 3 4 ]
        ]
        guitext "scale(%)"
        guislider entscale 0 200
        guibutton "update" [halflight $entscale 100; entscale = 100] 
    ]
    guibar
    guicheckbox "ambient" entguiattr4 1 0 [entupdate]
] [entscale = 100]

newentgui monster "direction" "0 360" [
    guilist [
        guilist [
            guiradio "ogro     " entguiattr1 0 [entupdate]
            guiradio "rhino    " entguiattr1 1 [entupdate] 
            guiradio "ratama   " entguiattr1 2 [entupdate]
            guiradio "slith    " entguiattr1 3 [entupdate]
            guiradio "bauul    " entguiattr1 4 [entupdate]
        ]
        guibar
        guilist [
            guiradio "hellpig  " entguiattr1 5 [entupdate]
            guiradio "knight   " entguiattr1 6 [entupdate]
            guiradio "goblin   " entguiattr1 7 [entupdate]
            guiradio "spider   " entguiattr1 8 [entupdate]
        ]
    ]
]

newentgui particles "type" "0 35"

newentgui spotlight "radius" "0 90"

newentgui playerstart "direction" "0 360" [@teamselect]

newentgui teleport "tag model" "0 20 0 200" [guicheckbox "use model" entguiattr1 0 -1 [entupdate]]

newentgui teledest "direction tag" "0 360 0 20" [guicheckbox "keep velocity" entguiattr2 1 0 [entupdate]]

entweapontypelist = "shells riflerounds bullets rockets grenades cartridges health greenarmour yellowarmour quaddamage healthboost"

entguiweapon = [
    guilistsplit n 2 $entweapontypelist [
        guibutton $n [entset @n]
    ]
]

setupweapongui = [
    loop i 11 [
        newentgui (at $entweapontypelist $i) "" "" [@entguiweapon]
    ]
]; setupweapongui

