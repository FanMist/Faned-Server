//Fanatic Edition: smc.cfg

createine = [if (! (getalias $arg1)) [$arg1 = $arg2]]

newgui smc [
    guinoautotab [
        createine _smc_path "faned/smc/"
        guilist [
            guitext "Set smc directory: " "info.jpg"
            guifield _smc_path -68
        ]
        guistrut 1
        guitext (concatword "Current geometry stored @ " $_smc_path) "info.jpg"
        guistrut 1
        _smc_files = ""
        loopfiles f $_smc_path "smc" [_smc_files = (concat $_smc_files $f)]
        _smc_files = (sortlist $_smc_files x y [<=s $x $y])
        looplist f $_smc_files [
            guibutton (concatword "Paste /" $f ".smc") [
                if (= $editing 1) [
                    importsmc (concatword $_smc_path @@f ".smc")
                    echo (concatword "^f8faned^f7|smc: Geometry " @@f ".smc pasted successfully.")
                ] [
                    echo "^f8faned^f7|smc: Editmode required."
                ]
            ]
        ]
    ]
]

