//Fanatic Edition: prefabs.cfg

createine = [if (! (getalias $arg1)) [$arg1 = $arg2]]

newgui prefabs [
    guinoautotab [
        createine _prefab_path "packages/prefab/"
        guilist [
            guitext "Set prefab directory: " "info.jpg"
            guifield _prefab_path -68
        ]
        guistrut 1
        guitext (concatword "Current geometry stored @ " $_prefab_path) "info.jpg"
        guistrut 1
        _prefab_files = ""
        loopfiles f $_prefab_path "obr" [_prefab_files = (concat $_prefab_files $f)]
        _prefab_files = (sortlist $_prefab_files x y [<=s $x $y])
        looplist f $_prefab_files [
            guibutton (concatword "Paste /" $f ".obr") [
                if (= $editing 1) [
                    pasteprefab (concatword (strreplace $_prefab_path "packages/" "") @@f)
                    echo (concatword "^f8faned^f7|prefab: Geometry " @@f ".obr pasted successfully.")
                ] [
                    echo "^f8faned^f7|prefab: Editmode required."
                ]
            ]
        ]
        if ($havesel) [
            guistrut 1
            guitext (concatword "Save selection as .obr file stored @ " $_prefab_path) "info.jpg"
            guistrut 1
            guilist [
               guitext "Set prefab name: " "info.jpg"
                guifield _obrname -15
                guibutton "Save prefab" [saveprefab (concatword (strreplace $_prefab_path "packages/" "") $_obrname)] "blank.png"
            ]
        ]
    ]
]

