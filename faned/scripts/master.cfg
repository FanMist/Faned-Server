//Fanatic Edition: master.cfg

newgui master [
    guilist [
        guitext "^f7Set privilege: " "info.jpg"
        if (ismaster (getclientnum)) [
            guibutton "^f7relinquish master " [setmaster 0]
            guibutton "^f7equality " [looplist i $listclients [setmaster 1 $i]]
            guibutton "^f7specall " [looplist i $listclients [spectator 1 $i]]
            guibutton "^f7unspecall " [looplist i $listclients [spectator 0 $i]]
        ] [
            guibutton "^f7claim master " [setmaster 1]
        ]
    ]
    guilist [
        guitext "^f7Set mastermode: " "info.jpg"
        mm = (getmastermode)
        guiradio "^f7open (^f00^f7) " mm 0 [mastermode 0]
        guiradio "^f7veto (^f71^f7) " mm 1 [mastermode 1]
        guiradio "^f7locked (^f22^f7) " mm 2 [mastermode 2]
        guiradio "^f7private (^f33^f7) " mm 3 [mastermode 3]
    ]
    guistrut 1
    looplist cn $listclients [
        guitext (concatword "^f7client: ^f7(^f2" $cn "^f7) ^f2" (getclientname $cn) "^f7 ")
        guilist [
            guibutton "^f7follow (goto) " [
            if (= $editing 0) [edittoggle]
                _goto_loop = [
                    goto @@cn
                    if (= $editing 1) [
                        sleep 0 [_goto_loop]
                    ] [_goto_loop = 0]
                ]
                _goto_loop
                echo (concatword "^f8faned^f7|goto: Following " (getclientname @@cn) " (" @@cn "); hit E (edittoggle) to stop.")
           ]
            guibutton "^f7follow (spec) " [
                spectator 1
                sleep 1000 [
                    follow @@cn
                    echo (concatword "^f8faned^f7|spec: Following " (getclientname @@cn) " (" @@cn "); type /spectator 0 to stop.")
                ]
            ]
            guibutton "^f7goto " [goto @cn]
            guibutton "^f7suicide " [
                echo (concatword "^f8faned^f7|suicide: Suiciding " (getclientname @cn) " (" @cn ") ...")
                if ((isspectator @cn)) [
                    spectator 0 @cn; spectator 1 @@cn
                ] [
                    spectator 1 @cn; spectator 0 @@cn
                ]
            ]
            if (=s (getclientteam $cn) "good") [guibutton "^f7switch evil " [setteam @cn "evil"]] [guibutton "^f7switch good " [setteam @cn "good"]]
            if ((isspectator $cn)) [guibutton "^f7unspectate " [spectator 0 @cn]] [guibutton "^f7spectate " [spectator 1 @cn]]
            guibutton "^f7kick " [kick @cn]
            if (ismaster $cn) [guibutton "^f7take master " [setmaster 0 @cn]] [guibutton "^f7give master " [setmaster 1 @cn]]
        ]
    ]
]
